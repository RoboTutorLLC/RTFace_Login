apply plugin: 'com.android.application'

android {

    compileSdkVersion 30
    buildToolsVersion '30.0.3'


    defaultConfig {
        applicationId "com.example.iris.login1"
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1

        //Previous was 1.7.5.7 => 1.7.5.6

        versionName "1.8.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        android {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {

        def BOOLEAN = "boolean"
        def STRING  = "String"
        def TRUE    = "true"
        def FALSE   = "false"

        def LANGUAGE_FEATURE_ID = "LANGUAGE_FEATURE_ID"
        def LANGUAGE_ENGLISH    = "\"LANG_EN\""
        def LANGUAGE_SWAHILI    = "\"LANG_SW\""

        debug {
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_ENGLISH
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        en {
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_ENGLISH
            signingConfig signingConfigs.android
        }

        sw {
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_SWAHILI
            signingConfig signingConfigs.android
        }
    }

    // define apk naming behavior
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "facelogin"
            def SEP = "."
            def buildType = variant.buildType.name
            def version = variant.versionName

            def newApkName = project + SEP + buildType + SEP + version + ".apk"
            outputFileName = new File("./../../../../../build/", newApkName)
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    //compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha4'
    testImplementation 'junit:junit:4.13.2'

}